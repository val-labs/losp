#|-*- mode:lisp -*-
exec ros -Q -- $0 "$@" # |#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp
  (eval-when (:compile-toplevel :load-toplevel :execute)
    (ql:quickload '(:hunchensocket :parenscript) :silent t))
  (push :hunchentoot-no-ssl *features*))

(defpackage :losp-chat
  (:use :cl :parenscript :hunchentoot :hunchensocket))
(in-package :losp-chat)

(defclass chat-room (websocket-resource)
  ((name :initarg :name :initform (error "Name this room!") :reader name))
  (:default-initargs :client-class 'user))

(defclass user (websocket-client)
  ((name :initarg :user-agent :reader name :initform (error "Name this user!"))))

(defvar *chat-rooms* (list (make-instance 'chat-room :name "/bongo")))

(defun find-room (request)
  (find (script-name request) *chat-rooms* :test #'string= :key #'name))

(pushnew 'find-room *websocket-dispatch-table*)

(defun broadcast (room message &rest args)
  (loop for peer in (clients room)
        do (send-text-message peer (apply #'format nil message args))))

(defmethod text-message-received ((room chat-room) user message)
  (format t "000:[~s]~%" (elt message 0))
  (let ((ch (elt message 0)))
    (cond ((eql ch #\*)
	   (format t "PING~%")
	   (send-text-message user "*"))
	  ((eql ch #\.)
	   (let ((msg (subseq message 1)))
	     (format t "PS0:[~s]~%" msg)
	     (let ((expr (read-from-string msg)))
	       (format t "PS1:[~s]~%" expr)
	       (let ((rslt (ps* expr)))
		 (format t "PS2:[~s]~%" rslt)
		 (broadcast room (concatenate
				  'string "." rslt))))))
	  (t
	   (let ((expr (read-from-string message)))
	     (format t "SV0:[~s]~%" expr)
	     (let ((rslt (write-to-string (eval expr))))
	       (format t "SV1:[~s]~%" rslt)
	       (broadcast room (concatenate
				'string "," rslt))))))))

(defmethod client-connected ((room chat-room) user)
  t) ;;(broadcast room "~a has joined ~a" (name user) (name room)))

(defmethod client-disconnected ((room chat-room) user)
  t) ;;(broadcast room "~a has left ~a" (name user) (name room)))

(define-easy-handler (css :uri "/s.css") ()
  (setf (content-type*) "text/css")
  (uiop:read-file-string "www/s.css"))

(define-easy-handler (bongo-html :uri "/bongo.html") ()
  (uiop:read-file-string "www/bongo.html"))

(defclass websocket-easy-acceptor (websocket-acceptor easy-acceptor) ()
  (:documentation "Special WebSocket easy acceptor"))

(defvar *default-port* 1234)
(defvar *server* (make-instance 'websocket-easy-acceptor
				:port *default-port*))

(defun main (&rest argv)
  (declare (ignorable argv))
  (start *server*)
  (let ((url (format nil "http://localhost:~s/bongo.html" *default-port*)))
    (format t "listening... - go to ~s~%" url)
    (uiop:run-program (list "open" url))
    (loop (sleep 60))))
